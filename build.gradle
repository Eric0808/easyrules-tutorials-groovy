//=============================================================================================
// U S A G E   ================================================================================
//=============================================================================================

task usage doLast { print """${'='*90}
Easy Rules Tutorials (https://github.com/j-easy/easy-rules/tree/master/easy-rules-tutorials)

NB: Use '--quiet' or '-q' to supress Gradle build output lines

 ./gradlew usage
    Prints the following usage to the console

 ./gradlew FizzBuzz
    Baseline FizzBuzz using code. (From: http://wiki.c2.com/?FizzBuzzTest)
    Prints the integers from 1 to 100.  For multiples of 3, print 'Fizz' instead 
    of the integer and for the multiples of 5, print 'Buzz'. For integers which are 
    multiples of both 3 and 5, print 'FizzBuzz'.
    
 ./gradlew FizzBuzzER
    FizzBuzz implementation using EasyRules.
    
 ./gradlew Simple
    Very simple EasyRules examples with one, always true, rule.
    
 ./gradlew HelloWorld -q
    Obligatory 'Hello, world' example where the input is evaluated by a rule.
    
 ./gradlew Shop -P person=Tommy -P age=15
    Rule to evaluate drinking age (US 21); Nmae and age can be passed in via the command line
    or system properties; Default is 'Tom' at age '17'.

 ./gradlew Fire -q
    Copied from DROOLS examples. Create some rooms with sprinklers, start some fires.
    The sprinklers will turn on and an alarm will be raised to notify the Fire Department.
    Put out the fires, the sprinklers will turn off and the alarm will be silenced.
 
 ./gradlew Discount -P transactions=5 -q
    Copied from DROOLS examples. A customer buys and returns various items. A discount
    is applied or removed depending on the total at the end of each transaction. 
    Transactions and item are generated randomly.

 ./gradlew clean
  Remove all reports and artifacts from './$project.buildDir.name'

${'='*90}
"""}

//============================================================================================
// P L U G I N S   ===========================================================================
//============================================================================================


apply plugin: 'groovy'


//=============================================================================================
// C O N F I G U R A T I O N  =================================================================
//=============================================================================================

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility

//=============================================================================================
// R e p o s i t o r i e s   &   D e p e n d e n c i e s   ====================================
//=============================================================================================

repositories {
 	mavenLocal()
 	mavenCentral() 
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots'  }

}

dependencies {

    def jeasy     = '3.0.0-SNAPSHOT' // May 2017
    def groovy    = '2.4.11'         // Apr 2017

	compile ([
        "org.jeasy:easy-rules-core:$jeasy",      
        "org.codehaus.groovy:groovy:$groovy"
    ])

}

sourceSets {

    compile {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }
}


//============================================================================================
// R U N   T U T O R I A L S   ===============================================================
//============================================================================================

tasks.withType(JavaExec) {
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
}

task FizzBuzz (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.fizzbuzz.FizzBuzz'
}

task FizzBuzzER (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.fizzbuzz.FizzBuzzWithEasyRules'
}
task Simple (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.simple.Launcher'
}

task HelloWorld (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.helloworld.Launcher'
}

task Fire (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.fire.Launcher'
}

task Shop (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.shop.Launcher'
    args = [
        findProperty('person') ?: 'Tom', 
        findProperty('age') ?: '17'
    ]
}

task Discount (dependsOn: 'classes', type: JavaExec) {
    main = 'org.jeasy.groovy.tutorials.discount.Launcher'
    args = [
        findProperty('transactions') ?: '5'
    ] 
}


